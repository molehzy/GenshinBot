.版本 2
.支持库 e2ee

.程序集 绑定
.子程序 绑定_添加
    .参数 QQ, 文本型
    .参数 Tag, 文本型
    .参数 Cookie, 文本型
    .参数 CN, 逻辑型
    .局部变量 gs, GenshinAPI
    .局部变量 res, 文本型
    .局部变量 data, 存取键值表
    .局部变量 data2, 存取键值表
    .局部变量 i, 整数型
    .局部变量 nickname, 文本型
    .局部变量 game_uid, 文本型

    日志 (“[绑定-添加] ” ＋ QQ ＋ “ 启动线程”)
    
    gs.置Cookie (Cookie)
    res ＝ gs.get_UserGameRolesByCookie (CN)
    
    .判断开始 (data.从JSON载入 (res, , , ))
        
        .判断开始 (CN)
            
            .计次循环首 (data.取数量 (“//list”), i)
                .如果真 (data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].game_biz”) ＝ “hk4e_cn”)
                    nickname ＝ data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].nickname”)
                    game_uid ＝ data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].game_uid”)
                    日志 (“[绑定-添加] ” ＋ QQ ＋ “ 获取到游戏信息 ” ＋ nickname ＋ “|” ＋ game_uid)
                    跳出循环 ()
                .如果真结束
                
            .计次循环尾 ()
            data.创建 (“game_uid”, game_uid, “cookie”, Cookie, “CN”, CN)
            
        .默认
            .计次循环首 (data.取数量 (“//list”), i)
                .如果真 (data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].game_biz”) ＝ “hk4e_global”)
                    nickname ＝ data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].nickname”)
                    game_uid ＝ data.取文本 (“//list[” ＋ 到文本 (i) ＋ “].game_uid”)
                    日志 (“[绑定-添加] ” ＋ QQ ＋ “ 获取到游戏信息 ” ＋ nickname ＋ “|” ＋ game_uid)
                    跳出循环 ()
                .如果真结束
                
            .计次循环尾 ()
            data.创建 (“game_uid”, game_uid, “cookie”, Cookie, “CN”, CN)
        .判断结束
        
        .判断开始 (game_uid ≠ “”)
            data2.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
            data2.置键值表 (“//” ＋ Tag, data, )
            
            .判断开始 (data2.另存文件 (绑定目录 ＋ QQ ＋ “.db”, 0))
                日志 (“[绑定-添加] ” ＋ QQ ＋ “ 绑定成功”)
                CQ.发送私聊消息 (QQ, “已绑定 ” ＋ nickname ＋ #换行符 ＋ “UID ” ＋ game_uid, 真)
            .默认
                CQ.发送私聊消息 (QQ, “数据文件保存失败”, 真)
            .判断结束
            
        .默认
            CQ.发送私聊消息 (QQ, “[get_UserGameRolesByCookie] 未找到绑定的原神账号”, 真)
        .判断结束
        
    .默认
        CQ.发送私聊消息 (QQ, “[get_UserGameRolesByCookie] ” ＋ res, 真)
    .判断结束
    

.子程序 绑定_删除
    .参数 QQ, 文本型
    .参数 Tag, 文本型
    .局部变量 data, 存取键值表
    .局部变量 tags, 文本型, , "0"
    .局部变量 i, 整数型

    日志 (“[绑定-删除] ” ＋ QQ ＋ “ 删除标签[” ＋ Tag ＋ “]”)
    
    data.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
    tags ＝ data.取键数组 (0)
    
    .计次循环首 (取数组成员数 (tags), i)
        .如果真 (tags [i] ＝ Tag)
            .如果真 (取数组成员数 (tags) ＝ 1)
                .判断开始 (删除文件 (绑定目录 ＋ QQ ＋ “.db”))
                    日志 (“[绑定-删除] ” ＋ QQ ＋ “ 成功删除数据文件”)
                    CQ.发送私聊消息 (QQ, “成功删除数据文件”, 真)
                    返回 ()
                    
                .默认
                    CQ.发送私聊消息 (QQ, “数据文件删除失败”, 真)
                    返回 ()
                .判断结束
                
            .如果真结束
            
            data.删除 (“//” ＋ tags [i])
            
            .判断开始 (data.另存文件 (绑定目录 ＋ QQ ＋ “.db”, 0))
                日志 (“[绑定-删除] ” ＋ QQ ＋ “ 成功删除标签[” ＋ Tag ＋ “]”)
                CQ.发送私聊消息 (QQ, “成功删除标签 ” ＋ Tag, 真)
                返回 ()
                
            .默认
                CQ.发送私聊消息 (QQ, “数据文件保存失败”, 真)
                返回 ()
            .判断结束
            
        .如果真结束
        
    .计次循环尾 ()
    
    日志 (“[绑定-删除] ” ＋ QQ ＋ “ 未找到标签[” ＋ Tag ＋ “]”)
    CQ.发送私聊消息 (QQ, “未找到标签 ” ＋ Tag, 真)
    

.子程序 绑定_读CK, 文本型
    .参数 QQ, 文本型
    .参数 Tag, 文本型
    .局部变量 data, 存取键值表

    data.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
    返回 (data.取文本 (“//” ＋ Tag ＋ “.cookie”))
    

.子程序 绑定_读UID, 文本型
    .参数 QQ, 文本型
    .参数 Tag, 文本型
    .局部变量 data, 存取键值表

    data.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
    返回 (data.取文本 (“//” ＋ Tag ＋ “.game_uid”))
    

.子程序 绑定_读CN, 逻辑型
    .参数 QQ, 文本型
    .参数 Tag, 文本型
    .局部变量 data, 存取键值表

    data.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
    返回 (data.取逻辑值 (“//” ＋ Tag ＋ “.CN”))
    

.子程序 绑定_是否存在, 逻辑型
    .参数 QQ, 文本型

    返回 (路径_是否为文件 (绑定目录 ＋ QQ ＋ “.db”))
    

.子程序 绑定_枚举, 整数型
    .参数 数组, 文本型, 参考 数组
    .局部变量 a, 整数型
    .局部变量 b, 文本型, , "0"

    a ＝ 文件_枚举 (绑定目录, “*.db”, b, 假, 真, 假)
    数组 ＝ b
    返回 (a)
    

.子程序 绑定_读所有Tag, 文本型
    .参数 QQ, 文本型
    .局部变量 data, 存取键值表
    .局部变量 Tags, 文本型, , "0"
    .局部变量 i, 整数型
    .局部变量 allTag, 文本型

    data.从文件载入 (绑定目录 ＋ QQ ＋ “.db”, 0)
    Tags ＝ data.取键数组 (0)
    .计次循环首 (取数组成员数 (Tags), i)
        allTag ＝ allTag ＋ 到文本 (i) ＋ “. ” ＋ Tags [i] ＋ “ | ” ＋ data.取文本 (“//” ＋ Tags [i] ＋ “.game_uid”) ＋ #换行符
    .计次循环尾 ()
    allTag ＝ 文本_删右边 (allTag, 1)
    返回 (allTag)
    

